" Vim filetype plugin file
" Language:      Raku
" Maintainer:    vim-perl <vim-perl@googlegroups.com>
" Homepage:      https://github.com/Raku/vim-raku
" Bugs/requests: https://github.com/Raku/vim-raku/issues
" Last Change:   2021-04-16
" Contributors:  Hinrik Ã–rn SigurÃ°sson <hinrik.sig@gmail.com>
"
" Based on ftplugin/perl.vim by Dan Sharp <dwsharp at hotmail dot com>

if exists("b:did_ftplugin") | finish | endif
let b:did_ftplugin = 1

" Make sure the continuation lines below do not cause problems in
" compatibility mode.
let s:save_cpo = &cpo
set cpo-=C

setlocal formatoptions-=t
setlocal formatoptions+=crqol
setlocal keywordprg=p6doc

setlocal comments=:#\|,:#=,:#
setlocal commentstring=#%s

" Provided by Ned Konz <ned at bike-nomad dot com>
"---------------------------------------------
setlocal include=\\<\\(use\\\|require\\)\\>
setlocal includeexpr=substitute(v:fname,'::','/','g')
setlocal suffixesadd=.rakumod,.rakudoc,.pm6,.pm
setlocal define=[^A-Za-z_]

" The following line changes a global variable but is necessary to make
" gf and similar commands work. Thanks to Andrew Pimlott for pointing out
" the problem. If this causes a problem for you, add an
" after/ftplugin/raku.vim file that contains
"       set isfname-=:
set isfname+=:
setlocal iskeyword=@,48-57,_,192-255,-

" Raku exposes its CompUnits through $*REPO, but mapping module names to
" compunit paths is nontrivial. Probably it's more convenient to rely on
" people using zef, which has a handy store of sources for modules it has
" installed.
func s:compareReverseFtime(a, b)
    let atime = getftime(a:a)
    let btime = getftime(a:b)
    return atime > btime ? -1 : atime == btime ? 0 : 1
endfunc

let &l:path = "lib,."
if exists('$RAKULIB')
    let &l:path = &l:path . "," . $RAKULIB
endif
let &l:path = &l:path . "," . join(
            \ sort(glob("~/.zef/store/*/*/lib", 0, 1), "s:compareReverseFtime"),
            \ ',')

" Convert ascii-based ops into their single-character unicode equivalent
if get(g:, 'raku_unicode_abbrevs', 0)
    iabbrev <buffer> !(<) âŠ„
    iabbrev <buffer> !(<=) âŠˆ
    iabbrev <buffer> !(>) âŠ…
    iabbrev <buffer> !(>=) âŠ‰
    iabbrev <buffer> !(cont) âˆŒ
    iabbrev <buffer> !(elem) âˆ‰
    iabbrev <buffer> != â‰ 
    iabbrev <buffer> (&) âˆ©
    iabbrev <buffer> (+) âŠ
    iabbrev <buffer> (-) âˆ–
    iabbrev <buffer> (.) âŠ
    iabbrev <buffer> (<) âŠ‚
    iabbrev <buffer> (<+) â‰¼
    iabbrev <buffer> (<=) âŠ†
    iabbrev <buffer> (>) âŠƒ
    iabbrev <buffer> (>+) â‰½
    iabbrev <buffer> (>=) âŠ‡
    iabbrev <buffer> (\|) âˆª
    iabbrev <buffer> (^) âŠ–
    iabbrev <buffer> (atomic) âš›
    iabbrev <buffer> (cont) âˆ‹
    iabbrev <buffer> (elem) âˆˆ
    iabbrev <buffer> * Ã—
    iabbrev <buffer> **0 â°
    iabbrev <buffer> **1 Â¹
    iabbrev <buffer> **2 Â²
    iabbrev <buffer> **3 Â³
    iabbrev <buffer> **4 â´
    iabbrev <buffer> **5 âµ
    iabbrev <buffer> **6 â¶
    iabbrev <buffer> **7 â·
    iabbrev <buffer> **8 â¸
    iabbrev <buffer> **9 â¹
    iabbrev <buffer> ... â€¦
    iabbrev <buffer> / Ã·
    iabbrev <buffer> << Â«
    iabbrev <buffer> <<[=]<< Â«=Â«
    iabbrev <buffer> <<[=]>> Â«=Â»
    iabbrev <buffer> <= â‰¤
    iabbrev <buffer> =~= â‰…
    iabbrev <buffer> >= â‰¥
    iabbrev <buffer> >> Â»
    iabbrev <buffer> >>[=]<< Â»=Â«
    iabbrev <buffer> >>[=]>> Â»=Â»
    iabbrev <buffer> Inf âˆ
    iabbrev <buffer> atomic-add-fetch âš›+=
    iabbrev <buffer> atomic-assign âš›=
    iabbrev <buffer> atomic-fetch âš›
    iabbrev <buffer> atomic-dec-fetch --âš›
    iabbrev <buffer> atomic-fetch-dec âš›--
    iabbrev <buffer> atomic-fetch-inc âš›++
    iabbrev <buffer> atomic-inc-fetch ++âš›
    iabbrev <buffer> atomic-sub-fetch âš›âˆ’=
    iabbrev <buffer> e ğ‘’
    iabbrev <buffer> o âˆ˜
    iabbrev <buffer> pi Ï€
    iabbrev <buffer> set() âˆ…
    iabbrev <buffer> tau Ï„
endif

" Undo the stuff we changed.
let b:undo_ftplugin = "setlocal fo< com< cms< inc< inex< def< isf< isk< kp< path<" .
        \         " | unlet! b:browsefilter"

" Restore the saved compatibility options.
let &cpo = s:save_cpo
unlet s:save_cpo
